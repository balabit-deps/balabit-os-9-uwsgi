#!/usr/bin/perl -w

=head1 NAME

dh_uwsgi - Add dependency on the ABI version of uWSGI

When writing a package that builds an uWSGI plugin
and is not built with uWSGI itself
(and hence depends on the exact binary version),
add ${uwsgi:Depends} to the package's Depends: field in debian control
and run dh_uwsgi.

If you use dh, you can run dh_uwsgi using:

  dh --with uwsgi

Otherwise run it in debian/rules after installing the plugins.

=cut

use strict;
use Debian::Debhelper::Dh_Lib;
use Digest::MD5 qw(md5_hex);

=head1 SYNOPSIS

B<dh_uwsgi> [S<I<debhelper options>>] [S<B<-V>[I<version>]>] [S<B<--> I<file>>]

=head1 DESCRIPTION

dh_uwsgi is a debhelper program
that is responsible for correctly setting the dependency
on uwsgi-abi-I<uwsgi-ABI-version> for a package.

=head1 OPTIONS

No special options of its own. Use standard debhelper options.

=head1 NOTES

Note that this command is not idempotent.
L<dh_prep(1)> should be called between invocations of this command.
Otherwise, it may cause multiple instances of the same text
to be added to maintainer scripts.

=cut

init();

my $RELEVANT_CONTENT = '/usr/lib/uwsgi/plugins/*.so';

my $abi;

sub get_abi_name() {
	$abi ||= md5_hex( `uwsgi-core --dot-h` );
	return 'uwsgi-abi-' . $abi;
}

foreach my $package ( @{ $dh{DOPACKAGES} } ) {
	#next if is_udeb($package);
	next unless glob( tmpdir($package) . $RELEVANT_CONTENT );
	addsubstvar($package, "uwsgi:Depends", get_abi_name() );
}

=head1 SEE ALSO

L<debhelper(1)>

This program is part of the Debian uWSGI package.

L<uwsgi(8)>

=head1 AUTHOR

Jonas Smedegaard <dr@jones.dk>

=cut
